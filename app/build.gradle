apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion Versions.compiledSdk
    defaultConfig {
        project.ext.appName = 'rocky'
        applicationId "pl.mobite.rocky"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "pl.mobite.rocky.MockTestRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "$project.ext.appName-dev"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", project.ext.appName
        }
    }

    dexOptions {
        javaMaxHeapSize = 3g
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':domain')

    implementation Dependencies.kotlinStdLib
    implementation Dependencies.appcompatLib
    implementation Dependencies.playServiceMapsLib
    implementation Dependencies.cardViewLib

    implementation Dependencies.lifecycleExtensionsLib
    implementation Dependencies.lifecycleViewModelsLib

    implementation Dependencies.rxKotlinLib
    implementation Dependencies.rxBindingLib
    implementation Dependencies.rxRelayLib

    implementation Dependencies.retrofitLib
    implementation Dependencies.retrofitGsonConverterLib
    implementation Dependencies.retrofitRxJava2AdapterLib

    testImplementation Dependencies.junitLib
    testImplementation Dependencies.mockitoLib

    androidTestImplementation Dependencies.testRunner
    androidTestImplementation Dependencies.testRules
    androidTestImplementation Dependencies.espressoLib
    androidTestImplementation('com.squareup.rx.idler:rx2-idler:0.9.0') {
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    }
}

task changeApkName() {
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def date = new Date().format("yyMMdd_hhmmss")
            def fileName = "${project.ext.appName}_${variant.name}_${variant.versionName}_${date}.apk"
            output.outputFileName = fileName
        }
    }
}
gradle.taskGraph.whenReady {
    changeApkName
}
